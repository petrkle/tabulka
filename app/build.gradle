apply plugin: 'com.android.application'
apply plugin: 'org.ajoberstar.grgit'

ext {
  gitVersionName = grgit.describe()
  dirty = !grgit.status().clean
  if(dirty) gitVersionName = gitVersionName + '-dirty'
  gitVersionCode = grgit.log().size()
}


task printVersionName() {
    println("$gitVersionName")
}

android {
    defaultConfig {
        versionName "${gitVersionName}"
    }
}

android {
    namespace = "cz.kle.tabulka"
    compileSdkVersion 36
    defaultConfig {
        applicationId "cz.kle.tabulka"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode gitVersionCode
        versionName "${gitVersionName}"
    }
    base.archivesName = "Tabulka"
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig = signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'org.apache.cordova:framework:14.0.1'
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.1.20'))
}


task generate(type: Exec, dependsOn: 'generateLocale') {
    commandLine 'perl', './generate.pl'
    workingDir '..'
}

task generateLocale(type: Exec) {
    commandLine 'make'
    workingDir '../po'
}

preBuild.dependsOn(generate)

def props = new Properties()

props.load(new FileInputStream("${System.env.HOME}/android-sec/tabulka.properties"))

android.signingConfigs.release.storeFile rootProject.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword
